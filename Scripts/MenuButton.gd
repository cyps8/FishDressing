extends Button

var scaleTween: Tween
var wobbleTween: Tween

@export var silentButton: bool = false
@export var disableForWeb: bool = false
@export var backButton: bool = false

func _ready():
	if OS.get_name() == "Web" && disableForWeb:
		disabled = true

	mouse_entered.connect(Callable(_Focused))
	mouse_exited.connect(Callable(_Unfocused))
	focus_entered.connect(Callable(_Focused))
	focus_exited.connect(Callable(_Unfocused))
	pressed.connect(Callable(_Pressed))

	if pivot_offset == Vector2(0, 0):
		pivot_offset = size/2

func _Focused():
	if disabled:
		return
	if scaleTween:
		scaleTween.stop()
	if wobbleTween:
		wobbleTween.stop()
	scaleTween = create_tween()
	scaleTween.tween_property(self, "scale", Vector2(1.1, 1.1), 0.2)
	wobbleTween = create_tween().set_loops(-1)
	wobbleTween.tween_property(self, "rotation", deg_to_rad(3), 0.15).set_ease(Tween.EASE_OUT).set_trans(Tween.TRANS_QUAD)
	wobbleTween.tween_property(self, "rotation", deg_to_rad(0), 0.15).set_ease(Tween.EASE_IN).set_trans(Tween.TRANS_QUAD)
	wobbleTween.tween_property(self, "rotation", deg_to_rad(-3), 0.15).set_ease(Tween.EASE_OUT).set_trans(Tween.TRANS_QUAD)
	wobbleTween.tween_property(self, "rotation", deg_to_rad(0), 0.15).set_ease(Tween.EASE_IN).set_trans(Tween.TRANS_QUAD)
	AudioPlayer.ins.PlaySound(1)
	if !has_focus():
		grab_focus()

func _Pressed():
	if !silentButton:
		if backButton:
			AudioPlayer.ins.PlaySound(3)
		else:
			AudioPlayer.ins.PlaySound(2)

func _Unfocused():
	if scaleTween:
		scaleTween.stop()
	if wobbleTween:
		wobbleTween.stop()
	scaleTween = create_tween()
	scaleTween.tween_property(self, "scale", Vector2(1.0, 1.0), 0.2)
	wobbleTween = create_tween()
	wobbleTween.tween_property(self, "rotation", deg_to_rad(0), 0.2).set_ease(Tween.EASE_OUT).set_trans(Tween.TRANS_QUAD)
